import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.10"
    }
}

plugins {
    id("org.jetbrains.kotlin.jvm") version "1.9.10"
    id("com.palantir.git-version") version "3.0.0"
}

dependencies {
    implementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    implementation 'net.portswigger.burp.extensions:montoya-api:1.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.python:jython-standalone:2.7.3'
    implementation 'com.sparkjava:spark-core:2.9.4'
    implementation 'org.slf4j:slf4j-simple:2.0.9'

    implementation 'io.ktor:ktor-server-core:2.3.4'
    implementation 'io.ktor:ktor-server-netty:2.3.4'
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions.jvmTarget.set(JvmTarget.JVM_17)
}

compileKotlin {
    kotlinOptions {
        // Configure the Kotlin compiler options
        allWarningsAsErrors = true
    }
}

repositories {
    mavenCentral()
}

def generatedVersionDir = "${buildDir}/generated-version"

sourceSets {
    main {
        java {
            srcDir 'kotlin'
        }
        resources {
            srcDirs = ['resources', 'lib/GQLSpection/src']
        }
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}

tasks.register('generateVersionProperties') {
    def propertiesFile = file "$generatedVersionDir/version.properties"
    propertiesFile.parentFile.mkdirs()
    def properties = new Properties()
    def details = versionDetails()
    version(details.lastTag)
    println "Version from git tag: $version"
    properties.setProperty("version", details.lastTag.toString())
    propertiesFile.withWriter { properties.store(it, null) }
}

processResources.dependsOn generateVersionProperties


// Define a function to create a Copy task for a React app
def createReactAppCopyTask(String appName) {
    def appPath = "lib/${appName.toLowerCase()}"
    def resourcesPath = "resources/static/${appName.toLowerCase()}"

    def rebuildTask = task("rebuild${appName}", type: Exec) {
        workingDir = project.file(appPath)
        commandLine 'npm', 'run', 'build'
    }

    def cleanBuildDirTask = task("clean${appName}BuildDir", type: Delete) {
        delete "${appPath}/build"
    }

    rebuildTask.dependsOn cleanBuildDirTask

    def copyResourcesTask = task("copy${appName}Resources", type: Copy) {
        from "${appPath}/build"
        into "${resourcesPath}"
    }

    def cleanResourcesTask = task("clean${appName}Resources", type: Delete) {
        delete "${resourcesPath}"
    }

    copyResourcesTask.dependsOn(cleanResourcesTask, rebuildTask)

    return copyResourcesTask
}

// Call the function for each React app you want to build
def graphiqlCopyTask = createReactAppCopyTask("GraphiQL")
def playgroundCopyTask = createReactAppCopyTask("Playground")
def voyagerCopyTask = createReactAppCopyTask("Voyager")

// Add the Copy tasks to the processResources task
processResources {
    dependsOn(graphiqlCopyTask, playgroundCopyTask, voyagerCopyTask)
}

// Add the Copy tasks to the build task
build {
    dependsOn(processResources)
}

defaultTasks "build"

// build standalone jar with bundled dependencies
jar {
    manifest {
        attributes('Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '))
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    destinationDirectory = rootDir
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
