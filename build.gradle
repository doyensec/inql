import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.10"
    }
}

plugins {
    id("org.jetbrains.kotlin.jvm") version "1.9.10"
    id("com.palantir.git-version") version "3.0.0"
}

dependencies {
    implementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    implementation 'net.portswigger.burp.extensions:montoya-api:1.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.python:jython-standalone:2.7.3'
    implementation 'com.sparkjava:spark-core:2.9.4'
    implementation 'org.slf4j:slf4j-simple:2.0.9'

    implementation 'io.ktor:ktor-server-core:2.3.4'
    implementation 'io.ktor:ktor-server-netty:2.3.4'
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions.jvmTarget.set(JvmTarget.JVM_17)
}

compileKotlin {
    kotlinOptions {
        // Configure the Kotlin compiler options
        allWarningsAsErrors = true
    }
}

repositories {
    mavenCentral()
}

def generatedVersionDir = "${buildDir}/generated-version"

sourceSets {
    main {
        java {
            srcDir 'kotlin'
        }
        resources {
            srcDirs = ['resources', 'lib/GQLSpection/src']
        }
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}

tasks.register('generateVersionProperties') {
    def propertiesFile = file "$generatedVersionDir/version.properties"
    propertiesFile.parentFile.mkdirs()
    def properties = new Properties()
    def details = versionDetails()
    version(details.lastTag)
    println "Version from git tag: $version"
    properties.setProperty("version", details.lastTag.toString())
    propertiesFile.withWriter { properties.store(it, null) }
}

processResources.dependsOn generateVersionProperties

task rebuildReactApp(type: Exec) {
    workingDir = project.file('lib/graphiql')
    commandLine 'npm', 'run', 'build'
}

task cleanReactAppBuildDir(type: Delete) {
    delete 'lib/graphiql/build'
}

rebuildReactApp.dependsOn cleanReactAppBuildDir

task copyReactAppResources(type: Copy) {
    from 'lib/graphiql/build'
    into 'resources/static/graphiql'
}

task cleanReactAppResources(type: Delete) {
    delete 'resources/static/graphiql'
}

copyReactAppResources {
    dependsOn(cleanReactAppResources, rebuildReactApp)
}

processResources {
    dependsOn copyReactAppResources
}

build {
    dependsOn(clean, rebuildReactApp, copyReactAppResources)
}

defaultTasks "build"

// build standalone jar with bundled dependencies
jar {
    manifest {
        attributes('Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '))
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    destinationDirectory = rootDir
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
